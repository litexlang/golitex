have fn g(x Z) N =:
    case x < 0: 7 * (-x) + 2:
        -x $in N
        7 * (-x) + 2 $in N
    case x >= 0: 7 * x + 1:
        x $in N
        7 * x + 1 $in N

prove forall x, y Z: x != y => g(x) != g(y):
    forall x Z: (7 * x + 1) % 7 = ((7 * x) % 7 + 1 % 7) % 7 = (0 + 1) % 7 = 1
    forall x Z: (7 * (-x) + 2) % 7 = ((7 * (-x)) % 7 + 2 % 7) % 7 = (0 + 2) % 7 = 2

    y < 0 or y >= 0

    cases g(x) != g(y):
        case x < 0:
            cases g(x) != g(y):
                case y < 0:
                    contra g(x) != g(y):
                        g(x) = g(y) = 7 * (-x) + 2 = 7 * (-y) + 2
                        x = (7 * (-x) + 2 - 2) / -7  = (7 * (-y) + 2 - 2) / -7 = y
                        impossible x = y
                case y >= 0:
                    contra g(x) != g(y):
                        g(x) % 7= (7 * (-x) + 2) % 7 = 2
                        g(y) % 7= (7 * y + 1) % 7 = 1
                        2 != 1
                        impossible g(x) % 7 != g(y) % 7

        case x >= 0:
            cases g(x) != g(y):
                case y >= 0:
                    contra g(x) != g(y):
                        g(x) = g(y) = 7 * x + 1 = 7 * y + 1
                        x = (7 * x + 1 - 1) / 7 = y = (7 * y + 1 - 1) / 7
                        impossible x = y
                case y < 0:
                    contra g(x) != g(y):
                        g(x) % 7= (7 * x + 1) % 7 = 1
                        g(y) % 7= (7 * (-y) + 2) % 7 = 2
                        1 != 2
                        impossible g(x) % 7 != g(y) % 7
