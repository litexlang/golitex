# have statements

# have by exist_prop
prove:
    exist_prop x N st exist_x_larger_than_1():
        x > 1

    exist 2 st $exist_x_larger_than_1()

    have x st $exist_x_larger_than_1()
    x > 1

    fn zero_func(x Z)Z:
        x = 0

    exist_prop f fn(Z)Z st exist_f_is_always_zero():
        forall x Z:
            f(x) = 0

    exist zero_func st $exist_f_is_always_zero()

    have f st $exist_f_is_always_zero()
    forall x Z:
        f(x) = 0

    exist_prop self_add fn(Z, Z)Z st exist_f_always_equal_to_add():
        forall x Z, y Z:
            self_add(x, y) = x + y

    exist + st $exist_f_always_equal_to_add()

# have sets by naive set theory
prove:
    have s set = {1, 2, 3}
    have s2 set = {x N : x > 5}

    s = {3, 2, 1}

    forall x s2:
        x > 5

# have objects from builtin sets
prove:
    have a N, b Z, c Q, d R
    have s set
    have s2 finite_set


# have objects from finite sets size > 0
prove:
    let s finite_set:
        count(s) = 1
    1 > 0
    count(s) > 0
    have a st $item_exists_in(s)
    a $in s


# have objectName st item_exists_in(setName)
# it has syntax sugar: have objectName setName
prove:
    let s set, a set
    know a $in s
    exist a st $item_exists_in(s)
    $item_exists_in(s)
    have b st $item_exists_in(s)
    b $in s

prove:
    {x R: x > 0} = {x R: x > 0}

    exist 1 st $item_exists_in({x R: x > 0})

    have a set = {x R: x > 0}

    forall x a:
        x > 0

    {y R: y > 0} = {x R: x > 0}

prove:
    have fn f(x R) R = x + 1
    f(2) = 3

    have fn g(x R) R =:
        case x = 2: 3
        case x != 2: 4

    have fn:
        h(x R) R:
            x > 0
            =>:
                h(x) > 1
        prove:
            do_nothing
        = 100

    forall x R: x > 0 => h(x) > 1
    h(1) > 1

    have fn:
        p(x R) R:
            x > 0
            =>:
                p(x) > x
        case 100 > x:
            do_nothing
        = 100
        case not 100 > x:
            x + 1 > x
        = x + 1

    forall x R: x > 0 => p(x) > x

prove:
    exist_prop f fn(R)R st tmp():
        forall x R: x > 0 => f(x) > 0
        forall x R: x <= 0 => f(x) = 0

    have fn g(x R) R =:
        case x > 0: x
        case x <= 0: 0

    forall x R: x > 0 => g(x) = x, x > 0, g(x) > 0
    forall x R: x <= 0 => g(x) = 0

    exist g st $tmp()

    have f st $tmp()
    
prove:
    exist_prop f fn(R)R st tmp():
        forall x R: f(x) > 0

    have fn g(x R) R =:
        case x > 0: x
        case x <= 0: 100

    prove forall x R: g(x) > 0:
        prove_case_by_case:
            g(x) > 0
            case x > 0:
                g(x) = x
                x > 0
            case x <= 0:
                g(x) = 100
                x <= 0

    exist g st $tmp()

